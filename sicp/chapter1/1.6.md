# Упражнение 1.6

Лиза П. Хакер не понимает, почему `if` должна быть особой формой. «Почему нельзя просто определить ее как обычную процедуру с помощью `cond`?» — спрашивает она. Лизина подруга Ева Лу Атор утверждает, что, разумеется, можно, и определяет новую версию `if`:

```scheme
(define (new-if predicate then-clause else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
```

Ева показывает Лизе новую программу:

```scheme
(new-if (= 2 3) 0 5)
; 5
(new-if (= 1 1) 0 5)
; 0
```

Обрадованная Лиза переписывает через new-if программу вычисления квадратного корня:

```scheme
(define (sqrt-iter guess x) 
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))
```

Что получится, когда Лиза попытается использовать эту процедуру для вычисления квадратных корней? Объясните.

## Решение

```scheme
(define (new-if predicate then-clause else-clause) 
  (cond (predicate then-clause)
        (else else-clause)))

(define (square x) (* x x))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (average x y)
  (/ (+ x y) 2))

(define (improve guess x) (average guess (/ x guess)))

(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))

(sqrt 9) ; infinite loop
```

Процедура `new-if` будет бесконечно вычислять `else-clause` из-за аппликативного порядка вычислений, потому как `else-clause` рекурсивно вызвает `new-if`.

Чтобы применить составную процедуру `new-if` к аргументам, или, другими словами, чтобы вычислить процедуру `new-if`, нужно вычислить все её операнды, присвоенные формальным параметрам `predicate`, `then-clause` и `else-clause`. Так работает аппликативный порядок вычислений.

Так как `else-clause` рекурсивно вызывает `new-if`, этот формальный параметр будет вычисляться бесконечно.

### Детальный разбор

```scheme
(sqrt 9) 
```

1) Восстанавливаем тело `sqrt`

```scheme
(sqrt-iter 1.0 x)
```

2) Заменяем формальный параметр `x` на `9`

```scheme
(sqrt-iter 1.0 9)
```

3) Восстанавливаем тело `sqrt-iter` 

```scheme
(new-if (good-enough? guess x)
        guess
        (sqrt-iter (improve guess x)
                   x))
```

Оператор - `new-if`

Операнд - `(good-enough? guess x)`

Операнд - `guess`

Операнд - `(sqrt-iter (improve guess x) x))`

4) Вычисляем операнд `(good-enough? guess x)`

Формальный параметр `predicate` является процедурой `(good-enough? guess x)`

4.1) Заменяем формальные параметры на аргументы

```scheme
(good-enough? 1.0 9)
```

4.2) Восстанавливаем тело `good-enough?`

```scheme
(< (abs (- (square guess) x)) 0.001)
```

4.3) Заменяем формальные параметры на аргументы

```scheme
(< (abs (- (square 1.0) 9)) 0.001)
```

4.4) Восстанавливаем тело `square` и заменяем параметр `x` на аргумент `1.0`

```scheme
(< (abs (- (* 1.0 1.0) 9)) 0.001)
```

4.5) Вычисляем операнд `(* 1.0 1.0)`

```scheme
(< (abs (- 1.0 9)) 0.001)
```

4.6) Вычисляем операнд `(- 1.0 9)`

```scheme
(< (abs -8.0) 0.001)
```

4.7) Вычисляем операнд `(abs -8.0)`

```scheme
(< 8.0 0.001)
```

4.8) Вычисляем операнд `(< 8.0 0.001)` и заменяем формальный параметр `predicate` на `#f`

```scheme
(new-if #f
        guess
        (sqrt-iter (improve guess x)
                   x))
```

5) Вычисляем операнд `guess` и заменяем формальный параметр `then-clause` на `1.0`

```scheme
(new-if #f
        1.0
        (sqrt-iter (improve guess x)
                   x))
```

6) Вычисляем операнд `(sqrt-iter (improve guess x) x))`

Попадаем в бесконечнюу рекурсию

### Дополнительно

В данном случае никакой разницы между `if` и `cond` нет, потому как причина в аппликативном порядке вычислений. Если написать процедуру `new-if` с `if` вместо `cond`, результат не изменится - будет бесконечная рекурсия.

```scheme
(define (new-if predicate then-clause else-clause) 
  (if predicate then-clause else-clause))

(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))

(sqrt 9) ; infinite loop
```

Если использовать `cond` вместо `if` напрямую в `sqrt-iter`, процедура будет работать точно так же

```scheme
(define (sqrt-iter guess x)
  (cond
    ((good-enough? guess x) guess)
    (else (sqrt-iter (improve guess x) x))))

```
